package compilador;

// Importación de clases
import java_cup.runtime.*;
import java.io.FileReader;
import producciones.*;
import java.util.Vector;
import java.lang.Object;
   
/* 
  Esta sección del código se agrega a la clase resultante
*/
parser code {:

    //Cuando se setea en true indica que hubo errores en la sintaxis
    boolean errores;
    
    /*Entrada: Es un mensaje de error de tipo String y el símbolo de tipo Object 
      Salida: No tiene
      Error encontrado.
      Se hace un Override al método report_error de la clase lr_parser de CUP 
    */
    public void report_error(String message, Object info) {
        StringBuilder m = new StringBuilder("Error");
        if (info instanceof java_cup.runtime.Symbol) {
            java_cup.runtime.Symbol s = ((java_cup.runtime.Symbol) info);
            errores = true;
            if (s.left >= 0) {                
                m.append(" in line "+(s.left+1));
                if (s.right >= 0)
                    m.append(", column "+(s.right+1));
            }
        }
        m.append(" : "+message);
        System.err.println(m);
    }
   
    /*Entrada: Es un mensaje de error de tipo String y el símbolo de tipo Object 
      Salida: No tiene 
      Error de donde el sistema no se puede recuper
       Se hace un Override al método report_fatal_error de la clase lr_parser de CUP
    */
    public void report_fatal_error(String message, Object info) {
        report_error(message, info);
    }

    /* Se ejecuta el método main para ejecutar el analizador léxico y el analizador
       sintáctico. Se envía como parámetro la tabla de símbolos.
    */
    public static void main(String[] args){
        try {
            AnalizadorSintactico asin = new AnalizadorSintactico(
                    new AnalizadorLexico( new FileReader(args[0])));
            Object result = asin.parse().value;
        } catch (Exception ex) {
            ex.printStackTrace();
        }
    }
:};
   

   
// ------------Declaracion de terminales y no terminales
   
/* TERMINALES
   Se listan todos los tokens que se vayan a utilizar para la elaboración de
   producciones, estos son obtenidos por el analizador lexico. 
*/
terminal Token IDENTIFIER;
terminal Token MINUS;
terminal Token PLUS;
terminal Token DIVIDE;
terminal Token MULTI;
terminal Token MODULE;
terminal Token POWER;
terminal Token PLUS_PLUS;
terminal Token MINUS_MINUS;
terminal Token GREATER;
terminal Token GREATER_EQUAL;
terminal Token MINOR;
terminal Token MINOR_EQUAL;
terminal Token EQUAL_EQUAL;
terminal Token DIFFERENT;
terminal Token AND;
terminal Token OR;
terminal Token OPEN_PAREN;
terminal Token CLOSE_PAREN;
terminal Token TRUE;
terminal Token FALSE;
terminal Token NULL;
terminal Token OPEN_BRACKET;
terminal Token CLOSE_BRACKET;
terminal Token COMMA;
terminal Token INT;
terminal Token FLOAT;
terminal Token BOOL;
terminal Token CHAR;
terminal Token STRING;
terminal Token EQUAL;
terminal Token FINAL;
terminal Token FOR;
terminal Token RETURN;
terminal Token READ;
terminal Token PRINT;
terminal Token NOT;
terminal Token OPEN_KEY;
terminal Token CLOSE_KEY;
terminal Token IF;
terminal Token ELSE;
terminal Token ELIF;
terminal Token BREAK;
terminal Token COMMENT;
terminal Token MAIN;
terminal Token INTEGER;
terminal Token DECIMAL;
terminal Token CHARCHAIN;
terminal Token CHARACTER;

/*NO TERMINALES 
  Se listas todos los no terminales que se elaboran en las producciones. Se utiliza Object
  para indicar que estos no tienen un tipo definido, pudiendo ser un entero o String.
*/

non terminal Operation operation;
non terminal Token relational_operator;
non terminal Operation additive_operation;
non terminal Token additive_operator;
non terminal Operation multiplicative_operation;
non terminal Token multiplicative_operator;
non terminal Operation unary_operation;
non terminal Token unary_operator;
non terminal Operation basic_operation;
non terminal Empty empty;
non terminal CreateVar create_var;
non terminal Type type;
non terminal Sentences sentences;
non terminal CallFunction call_function;
non terminal CreateArray createArray;
non terminal ArrayList arrayList;
non terminal ModifyArray modifyArrayElement;
non terminal Block block;
non terminal FunctionList create_function;
non terminal ParametersFunction parameters;
non terminal ParameterList parameter_list;
non terminal ForStructure for_structure;
non terminal If if_structure;
non terminal Sentences else_structure; 
non terminal Vector<Elif> elif_structure;
non terminal Main main_function;
non terminal AssignVar assing_var;
non terminal Negation negation;
non terminal InitProgram init_program;

   
/* -------------Precedencia y producciones
   
/*PRECEDENCIA

  En este caso se utiliza la precedencia para eliminar la ambiguedad que se genera en la producción type,
  al tener un INT que hace referencia al tipo entero y un INT[] que hace referencia al tipo arreglo, de igual manera para la ambiguedad que se presenta con 
  con el ELIF y el ELSE.
*/
  precedence right INT;
    precedence right CHAR;
  precedence right ELIF; 
  precedence right ELSE; 
  

/* PRODUCCIONES   
 
/* 
  El programa inicia con la producción init_program, esta indica que se pueden crear funciones y luego
  utilizar la función main, o solo hacer uso de esta última. A partir de esta se desencadena el uso
  de las demás producciones.
*/

    start with init_program;

    init_program ::= create_function:cf main_function:mf
                    {:
                      InitProgram ip = new InitProgram(mf, cf);
                      RESULT = ip;
                    :}
                   | main_function:mf
                    {:
                      FunctionList cf = new FunctionList();
                      InitProgram ip = new InitProgram(mf, cf);
                      RESULT = ip;
                    :}
                   ;

    main_function ::= INT MAIN OPEN_PAREN CLOSE_PAREN block:b
                      {:
                        Main m = new Main(b, null);
                        RESULT = m;
                      :}
                    ;

    create_function ::= create_function:cf type:t IDENTIFIER:id OPEN_PAREN parameters:p CLOSE_PAREN block:b
                        {:
                          Identifier ident = new Identifier( (String)id.getValue(), new int[] {id.getRow(), id.getColumn()});
                          Function f = new Function(t, ident, p, b, null);
                          if(cf == null) cf = new FunctionList();
                          cf.addFunction(f);
                          RESULT = cf;
                        :}
                      | type:t IDENTIFIER:id OPEN_PAREN parameters:p CLOSE_PAREN block:b
                        {:
                          Identifier ident = new Identifier( (String)id.getValue(), new int[] {id.getRow(), id.getColumn()});
                          Function f = new Function(t, ident, p, b, null);
                          FunctionList cf = new FunctionList();
                          cf.addFunction(f);
                          RESULT = cf;
                        :}
                      ;

    parameters ::= empty
                  {:
                    RESULT = new ParametersFunction();
                  :}
                 | parameters:pa COMMA type:t IDENTIFIER:id
                  {:
                    Identifier ident = new Identifier( (String)id.getValue(), new int[] {id.getRow(), id.getColumn()});
                    Parameters p = new Parameters(t, ident);
                    if(pa == null) pa = new ParametersFunction();
                    pa.addParameter(p);
                    RESULT = pa;

                  :}
                 | type:t IDENTIFIER:id
                  {:
                    Identifier ident = new Identifier( (String)id.getValue(), new int[] {id.getRow(), id.getColumn()});
                    Parameters p = new Parameters(t, ident);
                    ParametersFunction pa = new ParametersFunction();
                    pa.addParameter(p);
                    RESULT = pa;

                  :}
                 ;

    block ::=  OPEN_KEY sentences:s CLOSE_KEY
              {:
                  RESULT = new Block(s);
              :}
            |  error CLOSE_KEY
              {:
                RESULT = new Block(null);
              :}
            ; 

    sentences ::= empty
                 {:
                    RESULT = new Sentences();
                 :}
               | operation:op FINAL sentences:sent
                 {:
                    if(sent == null) sent = new Sentences();
                    OperationSentence opr = new OperationSentence(op);
                    sent.addSentence(opr);
                    RESULT = sent;
                 :}
               | create_var:op FINAL sentences:sent
                 {:
                    if(sent == null) sent = new Sentences();
                    sent.addSentence(op);
                    RESULT = sent;
                 :}
               | assing_var:op FINAL sentences:sent
                 {:
                    if(sent == null) sent = new Sentences();
                    sent.addSentence(op);
                    RESULT = sent;
                 :}
               | modifyArrayElement:op FINAL sentences:sent
                 {:
                    if(sent == null) sent = new Sentences();
                    sent.addSentence(op);
                    RESULT = sent;
                 :}
               | createArray:op FINAL sentences:sent
                 {:
                    if(sent == null) sent = new Sentences();
                    sent.addSentence(op);
                    RESULT = sent;
                 :}
               | if_structure:op sentences:sent
                 {:
                    if(sent == null) sent = new Sentences();
                    sent.addSentence(op);
                    RESULT = sent;
                 :}
               | FOR:fr OPEN_PAREN for_structure:fs CLOSE_PAREN block:bl sentences:sent
                 {:
                    if(sent == null) sent = new Sentences();
                    For ff = new For(fs, bl, new int[] {fr.getRow(), fr.getColumn()});
                    sent.addSentence(ff);
                    RESULT = sent;
                 :}
               | RETURN:rt operation:op FINAL sentences:sent
                 {:
                    if(sent == null) sent = new Sentences();
                    Return ff = new Return(op, new int[] {rt.getRow(), rt.getColumn()});
                    sent.addSentence(ff);
                    RESULT = sent;
                 :}
               | BREAK:br FINAL sentences:sent
                 {:
                    if(sent == null) sent = new Sentences();
                    Break ff = new Break(new int[] {br.getRow(), br.getColumn()});
                    sent.addSentence(ff);
                    RESULT = sent;
                 :}
               | PRINT:pr OPEN_PAREN operation:op CLOSE_PAREN FINAL sentences:sent
                 {:
                    if(sent == null) sent = new Sentences();
                    Print ff = new Print(op, new int[] {pr.getRow(), pr.getColumn()});
                    sent.addSentence(ff);
                    RESULT = sent;
                 :}
               | READ:pr OPEN_PAREN IDENTIFIER:id CLOSE_PAREN FINAL sentences:sent
                 {:
                    if(sent == null) sent = new Sentences();
                    Identifier identifier = new Identifier((String)id.getValue(), new int[] {id.getRow(), id.getColumn()});
                    Read ff = new Read(identifier, new int[] {pr.getRow(), pr.getColumn()});
                    sent.addSentence(ff);
                    RESULT = sent;
                 :}
               | error
                 {:
                    RESULT = new Sentences();
                 :}
               | COMMENT sentences:sent
                 {:
                    if(sent == null) sent = new Sentences();
                    RESULT = sent;
                 :}
               ;

    for_structure ::= create_var:cv FINAL operation:op1 FINAL operation:op2
                    {:
                      RESULT = new  ForStructure(cv, op1, op2); 
                    :}
                     ;

    if_structure ::= IF:iff negation OPEN_PAREN operation:op CLOSE_PAREN OPEN_KEY sentences:s1 CLOSE_KEY
                       	elif_structure:elf
		        else_structure:els
                        {:
                            RESULT = new If(op, s1, elf, els, new int[] {iff.getRow(), iff.getColumn()});
                        :}
                        ;

    else_structure ::= empty
                        {:
                           RESULT = null;
                        :}
		       |ELSE OPEN_KEY sentences:sent CLOSE_KEY
                        {:
                           RESULT = sent;
                        :}
		       ;

    elif_structure ::= empty
                        {:
                          RESULT = new Vector<Elif>(); 
                        :}
                       | ELIF:elf negation OPEN_PAREN operation:op1 CLOSE_PAREN OPEN_KEY sentences:s1 CLOSE_KEY
                        {:
                            Vector<Elif> al = new Vector<Elif>();
                            Elif elf1 = new Elif(op1, s1, new int[] {elf.getRow(), elf.getColumn()});
                            al.add(elf1);
                            RESULT = al;
                        :}
		       | elif_structure:elfs ELIF:el negation OPEN_PAREN operation:op1 CLOSE_PAREN OPEN_KEY sentences:s1 CLOSE_KEY
                         {:
                            if(elfs == null) elfs = new Vector<Elif>();
                            Elif elf = new Elif(op1, s1, new int[] {el.getRow(), el.getColumn()});
                            elfs.add(elf);
                            RESULT = elfs;
                         :}
                        ;


    operation ::= operation:op relational_operator:ro additive_operation:ao
                  {:
                    if( ((String) ro.getValue()) == ">") RESULT = new Greater(op, ao, new int[] {ro.getRow(), ro.getColumn()});
                    if( ((String) ro.getValue()) == ">=") RESULT = new GreaterEqual(op, ao, new int[] {ro.getRow(), ro.getColumn()});
                    if( ((String) ro.getValue()) == "<") RESULT = new Minor(op, ao, new int[] {ro.getRow(), ro.getColumn()});
                    if( ((String) ro.getValue()) == "<=") RESULT = new MinorEqual(op, ao, new int[] {ro.getRow(), ro.getColumn()});
                    if( ((String) ro.getValue()) == "==") RESULT = new EqualEqual(op, ao, new int[] {ro.getRow(), ro.getColumn()});
                    if( ((String) ro.getValue()) == "!=") RESULT = new Different(op, ao, new int[] {ro.getRow(), ro.getColumn()});                    
                  :}
                | additive_operation:ao
                  {:
                    RESULT = ao;
                  :}
                ;

    relational_operator ::= GREATER:g
                            {:
                              RESULT = g;
                            :}
                          | GREATER_EQUAL:ge
                            {:
                              RESULT = ge;
                            :}
                          | MINOR:m
                            {:
                              RESULT = m;
                            :}
                          | MINOR_EQUAL:me
                            {:
                              RESULT = me;
                            :}
                          | EQUAL_EQUAL:ee
                            {:
                              RESULT = ee;
                            :}
                          | DIFFERENT:d
                            {:
                              RESULT = d;
                            :}
                          ;

    additive_operation ::= additive_operation:ao additive_operator:aop multiplicative_operation:mo
                          {:
                            if( ((String) aop.getValue()) == "-") RESULT = new Minus(ao, mo, new int[] {aop.getRow(), aop.getColumn()});
                            if( ((String) aop.getValue()) == "+") RESULT = new Plus(ao, mo, new int[] {aop.getRow(), aop.getColumn()});
                            if( ((String) aop.getValue()) == "|") RESULT = new Or(ao, mo, new int[] {aop.getRow(), aop.getColumn()});                
                          :}
                         | multiplicative_operation:mo
                          {:
                            RESULT = mo;
                          :}
                         ;

    additive_operator   ::= MINUS:m
                            {:
                              RESULT = m;
                            :}
                          | PLUS:p
                            {:
                              RESULT = p;
                            :}
                          | OR:o
                            {:
                              RESULT = o;
                            :}
                          ;    

    multiplicative_operation ::= multiplicative_operation:mo multiplicative_operator:mop unary_operation:uo
                                {:
                                  if( ((String) mop.getValue()) == "/") RESULT = new Divide(mo,uo, new int[] {mop.getRow(), mop.getColumn()});
                                  if( ((String) mop.getValue()) == "*") RESULT = new Multi(mo, uo, new int[] {mop.getRow(), mop.getColumn()});
                                  if( ((String) mop.getValue()) == "**") RESULT = new Power(mo, uo, new int[] {mop.getRow(), mop.getColumn()});
                                  if( ((String) mop.getValue()) == "~") RESULT = new Module(mo, uo, new int[] {mop.getRow(), mop.getColumn()});
                                  if( ((String) mop.getValue()) == "&") RESULT = new And(mo, uo, new int[] {mop.getRow(), mop.getColumn()});                   
                                :}
                               | unary_operation:uo
                                {:
                                  RESULT = uo;
                                :}
                               ;

    multiplicative_operator ::= DIVIDE:d
                                {:
                                  RESULT = d;
                                :}
                              | MULTI:m
                                {:
                                  RESULT = m;
                                :}
                              | POWER:p
                                {:
                                  RESULT = p;
                                :}
                              | MODULE:mo
                                {:
                                  RESULT = mo;
                                :}
                              | AND:a
                                {:
                                  RESULT = a;
                                :}
                              ; 

    unary_operation ::= unary_operator:uop unary_operation:uo
                        {:
                          if( ((String) uop.getValue()) == "--") RESULT = new MinusMinus(uo, new int[] {uop.getRow(), uop.getColumn()});
                          if( ((String) uop.getValue()) == "++") RESULT = new PlusPlus(uo, new int[] {uop.getRow(), uop.getColumn()});
                          if( ((String) uop.getValue()) == "-") RESULT = new MinusUnary(uo, new int[] {uop.getRow(), uop.getColumn()});                
                        :}
                      | basic_operation:ba
                        {:
                          RESULT = ba;
                        :}
                      ;

    unary_operator ::= MINUS_MINUS:mm
                      {:
                        RESULT = mm;
                      :}
                     | PLUS_PLUS:pp
                      {:
                        RESULT = pp;
                      :}
                     | MINUS:m
                      {:
                        RESULT = m;
                      :}
                     ;

    basic_operation ::= INTEGER:i
                        {:
                            RESULT = new IntLiteral((int)i.getValue(), new int[] {i.getRow(), i.getColumn()});
                        :}
                      | DECIMAL:f
                        {:
                            RESULT = new FloatLiteral((float)f.getValue(), new int[] {f.getRow(), f.getColumn()});
                        :}
                      | CHARCHAIN:s
                        {:
                            RESULT = new StringLiteral((String)s.getValue(), new int[] {s.getRow(), s.getColumn()});
                        :}
                      | CHARACTER:c
                        {:
                            RESULT = new CharLiteral(c.getValue().toString().charAt(1), new int[] {c.getRow(), c.getColumn()});
                        :}
                      | IDENTIFIER:id
                        {:
                            RESULT = new Identifier((String)id.getValue(), new int[] {id.getRow(), id.getColumn()});
                        :}
                      | TRUE:b
                        {:
                            RESULT = new BoolLiteral(true, new int[] {b.getRow(), b.getColumn()});
                        :}
                      | FALSE:b
                        {:
                            RESULT = new BoolLiteral(false, new int[] {b.getRow(), b.getColumn()});
                        :}
                      | negation OPEN_PAREN operation:op CLOSE_PAREN
                        {:
                            RESULT = op;
                        :} 
                      | IDENTIFIER:id OPEN_BRACKET INTEGER:i CLOSE_BRACKET
                        {:
                            RESULT = new ArrayPositionOperation((String)id.getValue(), (int)i.getValue(),new int[] {id.getRow(), id.getColumn()});
                        :} 
                      | OPEN_BRACKET  arrayList:al CLOSE_BRACKET 
                        {:
                            RESULT = new ArrayListAssigment(al);
                        :} 
                      | call_function:cf
                        {:
                            RESULT = cf;
                        :} 
                      | NULL:nl
                        {:
                            RESULT = new NullLiteral(null, new int[] {nl.getRow(), nl.getColumn()});
                        :} 
                      ;

   parameter_list ::= parameter_list:pl COMMA operation:op
                {:
                        if(pl == null) pl = new ParameterList();
                    pl.AddOperation(op);
                    RESULT = pl;
                 :}
                     | operation:op
                {:
                    ParameterList pl = new ParameterList();
                    pl.AddOperation(op);
                    RESULT = pl;
                 :}
                     ;

    call_function ::= IDENTIFIER:id OPEN_PAREN parameter_list:pl CLOSE_PAREN
                {:
                    Identifier identifier = new Identifier((String)id.getValue(), new int[] {id.getRow(), id.getColumn()});
                    RESULT = new CallFunction(identifier.getName(), pl, new int[] {id.getRow(), id.getColumn()});
                :}
                    | IDENTIFIER:id OPEN_PAREN CLOSE_PAREN
                {:
                    Identifier identifier = new Identifier((String)id.getValue(), new int[] {id.getRow(), id.getColumn()});
                    RESULT = new CallFunction(identifier.getName(), null, new int[] {id.getRow(), id.getColumn()});
                :}
                    ;

    arrayList ::= empty
                 {:
                    RESULT = new ArrayList();
                 :}
                 | operation:op
                 {:
                    ArrayList al = new ArrayList();
                    al.AddOperation(op);
                    RESULT = al;
                 :}
                 | operation:op COMMA arrayList:al
                 {:
                    if(al == null) al = new ArrayList();
                    al.AddOperation(op);
                    RESULT = al;
                 :}
                 ;

    empty ::=
            ;

    modifyArrayElement ::= IDENTIFIER:id OPEN_BRACKET INTEGER:i CLOSE_BRACKET EQUAL operation:op
                {:
                    Identifier identifier = new Identifier((String)id.getValue(), new int[] {id.getRow(), id.getColumn()});
                    RESULT = new ModifyArray(identifier, (int)i.getValue(), op, new int[] {id.getRow(), id.getColumn()});
                :}
                ;

    createArray ::= type:t IDENTIFIER:id OPEN_BRACKET INTEGER:i CLOSE_BRACKET
                {:
                    Identifier identifier = new Identifier((String)id.getValue(), new int[] {id.getRow(), id.getColumn()});
                    RESULT = new CreateArray(t, identifier, (int)i.getValue(), null, new int[] {id.getRow(), id.getColumn()});
                :}
                | type:t IDENTIFIER:id OPEN_BRACKET CLOSE_BRACKET EQUAL OPEN_BRACKET arrayList:al CLOSE_BRACKET
                {:
                    Identifier identifier = new Identifier((String)id.getValue(), new int[] {id.getRow(), id.getColumn()});
                    RESULT = new CreateArray(t, identifier, 0, al, new int[] {id.getRow(), id.getColumn()});
                :}
                ;


    create_var ::= type:t IDENTIFIER:id EQUAL operation:op
                {:
                    Identifier identifier = new Identifier((String)id.getValue(), new int[] {id.getRow(), id.getColumn()});
                    RESULT = new CreateVar((Type)t, identifier, op, new int[] {id.getRow(), id.getColumn()});
                :}
                 | type:t IDENTIFIER:id
                {:
                    Identifier identifier = new Identifier((String)id.getValue(), new int[] {id.getRow(), id.getColumn()});
                    RESULT = new CreateVar(t, identifier, null, new int[] {id.getRow(), id.getColumn()});
                :}
                 ;

    assing_var ::= IDENTIFIER:id EQUAL operation:op
                {:
                    Identifier identifier = new Identifier((String)id.getValue(), new int[] {id.getRow(), id.getColumn()});
                    RESULT = new AssignVar(identifier, op, new int[] {id.getRow(), id.getColumn()});
                :}
                ;


    negation ::= empty
		| NOT
		;


    type ::= INT
            {:
                RESULT = new IntType();
            :}
           | FLOAT
           {:
                RESULT = new FloatType();
            :}
           | CHAR
            {:
                RESULT = new CharType();
            :}
           | STRING
           {:
                RESULT = new StringType();
            :}
           | BOOL
           {:
                RESULT = new BooleanType();
            :}
           ;
